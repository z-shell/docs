builtin setopt localoptions noerrreturn noerrexit extendedglob warncreateglobal typesetsilent noshortloops unset
.zi-add-report "${ZI[CUR_USPL2]}" "Zle $*"
typeset -a pos
pos=( "$@" )
builtin set -- "${@:#--}"
if [[ ( $1 = -N && ( $# = 2 || $# = 3 ) ) || ( $1 = -C && $# = 4 ) ]]; then
    if [[ ${ZI_ZLE_HOOKS_LIST[$2]} = 1 ]]; then
      local quoted="$2"
      quoted="${(q)quoted}"
      [[ -n ${ZI[CUR_USPL2]} ]] && ZI[WIDGETS_DELETE__${ZI[CUR_USPL2]}]+="$quoted "
      [[ ${ZI[DTRACE]} = 1 ]] && ZI[WIDGETS_DELETE___dtrace/_dtrace]+="$quoted "
    elif (( ${+widgets[$2]} )); then
      local widname="$2" targetfun="${${${(M)1:#-C}:+$4}:-$3}"
      local completion_widget="${${(M)1:#-C}:+$3}"
      local saved_widcontents="${widgets[$widname]}"
      widname="${(q)widname}"
      completion_widget="${(q)completion_widget}"
      targetfun="${(q)targetfun}"
      saved_widcontents="${(q)saved_widcontents}"
      local quoted="$1 $widname $completion_widget $targetfun $saved_widcontents"
      quoted="${(q)quoted}"
      [[ -n ${ZI[CUR_USPL2]} ]] && ZI[WIDGETS_SAVED__${ZI[CUR_USPL2]}]+="$quoted "
      [[ ${ZI[DTRACE]} = 1 ]] && ZI[WIDGETS_SAVED___dtrace/_dtrace]+="$quoted "
    else
      .zi-add-report "${ZI[CUR_USPL2]}" "Note: a new widget created via zle -N: \`$2'"
      local quoted="$2"
      quoted="${(q)quoted}"
      [[ -n ${ZI[CUR_USPL2]} ]] && ZI[WIDGETS_DELETE__${ZI[CUR_USPL2]}]+="$quoted "
      [[ ${ZI[DTRACE]} = 1 ]] && ZI[WIDGETS_DELETE___dtrace/_dtrace]+="$quoted "
    fi
fi
builtin zle "${pos[@]}"
return $?
