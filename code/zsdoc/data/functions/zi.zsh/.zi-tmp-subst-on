local mode="$1"
[[ ${ZI[TMP_SUBST]} != inactive ]] && builtin return 0
ZI[TMP_SUBST]="$mode"
builtin unset "ZI[bkp-autoload]" "ZI[bkp-compdef]"
if [[ $mode != compdef ]]; then
  (( ${+functions[autoload]} )) && ZI[bkp-autoload]="${functions[autoload]}"
  functions[autoload]=':zi-tmp-subst-autoload "$@";'
fi
(( ${+functions[compdef]} )) && ZI[bkp-compdef]="${functions[compdef]}"
functions[compdef]=':zi-tmp-subst-compdef "$@";'
if [[ -n ${ICE[subst]} ]] {
  (( ${+functions[source]} )) && ZI[bkp-source]="${functions[source]}"
  (( ${+functions[.]} )) && ZI[bkp-.]="${functions[.]}"
  (( ${+functions[.zi-service]} )) || builtin source "${ZI[BIN_DIR]}/lib/zsh/additional.zsh"
  functions[source]=':zi-tmp-subst-source "$@";'
  functions[.]=':zi-tmp-subst-source "$@";'
}
[[ ( $mode = light && ${+ICE[trackbinds]} -eq 0 ) || $mode = compdef ]] && return 0
builtin unset "ZI[bkp-bindkey]" "ZI[bkp-zstyle]" "ZI[bkp-alias]" "ZI[bkp-zle]"
(( ${+functions[bindkey]} )) && ZI[bkp-bindkey]="${functions[bindkey]}"
functions[bindkey]=':zi-tmp-subst-bindkey "$@";'
[[ $mode = light-b || ( $mode = light && ${+ICE[trackbinds]} -eq 1 ) ]] && return 0
(( ${+functions[zstyle]} )) && ZI[bkp-zstyle]="${functions[zstyle]}"
functions[zstyle]=':zi-tmp-subst-zstyle "$@";'
(( ${+functions[alias]} )) && ZI[bkp-alias]="${functions[alias]}"
functions[alias]=':zi-tmp-subst-alias "$@";'
(( ${+functions[zle]} )) && ZI[bkp-zle]="${functions[zle]}"
functions[zle]=':zi-tmp-subst-zle "$@";'
builtin return 0
