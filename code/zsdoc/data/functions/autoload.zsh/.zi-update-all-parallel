emulate -LR zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops nomonitor nonotify
local id_as repo snip uspl user plugin PUDIR="$(mktemp -d)"
local -A PUAssocArray map
map=( / --  "=" -EQ-  "?" -QM-  "&" -AMP-  : - )
local -a files
integer main_counter counter PUPDATE=1
files=( ${ZI[SNIPPETS_DIR]}/**/(._zi|._zinit|._zplugin)/mode(ND) )
main_counter=${#files}
if (( OPTS[opt_-s,--snippets] || !OPTS[opt_-l,--plugins] )) {
  for snip ( "${files[@]}" ) {
    main_counter=main_counter-1
    [[ ! -f ${snip:h}/url ]] && continue
    [[ -f ${snip:h}/id-as ]] && id_as="$(<${snip:h}/id-as)" || id_as=
    counter+=1
    local ef_id="${id_as:-$(<${snip:h}/url)}"
    local PUFILEMAIN=${${ef_id#/}//(#m)[\/=\?\&:]/${map[$MATCH]}}
    local PUFILE=$PUDIR/${counter}_$PUFILEMAIN.out
    .zi-update-or-status-snippet "$st" "$ef_id" &>| $PUFILE &
    PUAssocArray[$!]=$PUFILE
    .zi-wait-for-update-jobs snippets
  }
}

counter=0
PUAssocArray=()
if (( OPTS[opt_-l,--plugins] || !OPTS[opt_-s,--snippets] )) {
  local -a files2
  files=( ${ZI[PLUGINS_DIR]}/*(ND/) )
  for repo ( $files ) {
    uspl=${repo:t}
    [[ $uspl = custom || $uspl = _local---zi ]] && continue
    if [[ -f $repo/.git/config ]] {
      local -a config
      config=( ${(f)"$(<$repo/.git/config)"} )
      if [[ ${#${(M)config[@]:#\[remote[[:blank:]]*\]}} -eq 0 ]] {
        continue
      }
    }
    .zi-any-to-user-plugin "$uspl"
    local user=${reply[-2]} plugin=${reply[-1]}
    if [[ ! -d $repo/.git && ! -f $repo/._zi/is_release ]] {
      continue
    }
    files2+=( $repo )
  }
  main_counter=${#files2}
  for repo ( "${files2[@]}" ) {
    main_counter=main_counter-1
    uspl=${repo:t}
    id_as=${uspl//---//}
    counter+=1
    local PUFILEMAIN=${${id_as#/}//(#m)[\/=\?\&:]/${map[$MATCH]}}
    local PUFILE=$PUDIR/${counter}_$PUFILEMAIN.out
    .zi-any-colorify-as-uspl2 "$uspl"
    +zi-message "Updating $REPLY{â€¦}" >| $PUFILE
    .zi-any-to-user-plugin "$uspl"
    local user=${reply[-2]} plugin=${reply[-1]}
    .zi-update-or-status update "$user" "$plugin" &>>&|$PUFILE &
    PUAssocArray[$!]=$PUFILE
    .zi-wait-for-update-jobs plugins
  }
}
