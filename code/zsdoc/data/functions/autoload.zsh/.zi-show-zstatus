builtin setopt localoptions nullglob extendedglob nokshglob noksharrays

local infoc="${ZI[col-info2]}"

+zi-message "Home directory:        {file}${ZI[HOME_DIR]}{rst}"
+zi-message "Binary directory:      {file}${ZI[BIN_DIR]}{rst}"
+zi-message "Plugin directory:      {file}${ZI[PLUGINS_DIR]}{rst}"
+zi-message "Snippet directory:     {file}${ZI[SNIPPETS_DIR]}{rst}"
+zi-message "Service directory:     {file}${ZI[SERVICES_DIR]}{rst}"
+zi-message "Modules directory:     {file}${ZI[ZMODULES_DIR]}{rst}"
+zi-message "Completions directory: {file}${ZI[COMPLETIONS_DIR]}{rst}"
+zi-message "Loaded plugins: {num}$(( ${#ZI_REGISTERED_PLUGINS[@]} - 1 )){rst}"
integer light=0
local s
for s in "${(@v)ZI[(I)STATES__*]}"; do
  [[ "$s" = 1 ]] && (( light ++ ))
done
+zi-message "Light loaded: {num}$(( light - 1 )){rst}"
typeset -a plugins
plugins=( "${ZI[PLUGINS_DIR]}"/*(DN) )
+zi-message "Downloaded plugins: {num}$(( ${#plugins} - 1 )){rst}"
typeset -a completions
completions=( "${ZI[COMPLETIONS_DIR]}"/_[^_.]*~*.zwc(DN) )
+zi-message "Enabled completions: {num}${#completions[@]}{rst}"
completions=( "${ZI[COMPLETIONS_DIR]}"/[^_.]*~*.zwc(DN) )
+zi-message "Disabled completions: {num}${#completions[@]}{rst}"
completions=( "${ZI[PLUGINS_DIR]}"/*/**/_[^_.]*~*(*.zwc|*.html|*.txt|*.png|*.jpg|*.jpeg|*.js|*.md|*.yml|*.ri|_zsh_highlight*|/zsdoc/*|*.ps1)(DN) )
+zi-message "Completions available overall: {num}${#completions[@]}{rst}"
+zi-message -n "Snippets loaded: "
local sni
for sni in ${(onv)ZI_SNIPPETS[@]}; do
  +zi-message -n "{url}${sni% <[^>]#>}{rst} ${(M)sni%<[^>]##>}, "
done
[[ -z $sni ]] && builtin print -n " "
builtin print '\b\b  '
typeset -a matches m
integer count=0
matches=( ${ZI[PLUGINS_DIR]}/*/*.zwc(DN) )
local cur_plugin="" uspl1
for m in "${matches[@]}"; do
  uspl1="${${m:h}:t}"
  if [[ "$cur_plugin" != "$uspl1" ]]; then
    (( count ++ ))
    cur_plugin="$uspl1"
  fi
done
+zi-message "Compiled plugins: {num}$count{rst}"
