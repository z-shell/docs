local uspl2="$1"
typeset -a tmp
builtin setopt localoptions extendedglob nokshglob noksharrays
[[ "${ZI[PARAMETERS_BEFORE__$uspl2]}" != *[$'! \t']* || "${ZI[PARAMETERS_AFTER__$uspl2]}" != *[$'! \t']* ]] && return 1
typeset -A params_before params_after
params_before=( "${(z)ZI[PARAMETERS_BEFORE__$uspl2]}" )
params_after=( "${(z)ZI[PARAMETERS_AFTER__$uspl2]}" )
typeset -A params_pre params_post
params_pre=( )
params_post=( )
local key
typeset -aU keys
keys=( "${(k)params_after[@]}" );
keys=( "${keys[@]}" "${(k)params_before[@]}" );
for key in "${keys[@]}"; do
key="${(Q)key}"
[[ "${params_after[$key]}" = *local* ]] && continue
if [[ "${params_after[$key]}" != "${params_before[$key]}" ]]; then
  [[ -z "${params_before[$key]}" ]] && params_before[$key]="\"\""
  params_pre[$key]="${params_before[$key]}"
  [[ -z "${params_after[$key]}" ]] && params_after[$key]="\"\""
  params_post[$key]="${params_after[$key]}"
  fi
done
ZI[PARAMETERS_PRE__$uspl2]="${(j: :)${(qkv)params_pre[@]}}"
ZI[PARAMETERS_POST__$uspl2]="${(j: :)${(qkv)params_post[@]}}"
return 0
