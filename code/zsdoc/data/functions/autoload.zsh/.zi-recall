emulate -LR zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops
local -A ice
local el val cand1 cand2 local_dir filename is_snippet
local -a ice_order nval_ices output
ice_order=(
  ${(s.|.)ZI[ice-list]}
  ${(@)${(@Akons:|:u)${ZI_EXTS[ice-mods]//\'\'/}}/(#s)<->-/}
)
nval_ices=(
  ${(s.|.)ZI[nval-ice-list]}
  ${(@)${(@)${(@Akons:|:u)ZI_EXTS[ice-mods]}:#*\'\'*}/(#s)<->-/}
  svn
)
.zi-compute-ice "$1${${1:#(%|/)*}:+${2:+/}}$2" "pack" ice local_dir filename is_snippet || return 1
[[ -e $local_dir ]] && {
  for el ( ${ice_order[@]} ) {
    val="${ice[$el]}"
    cand1="${(qqq)val}"
    cand2="${(qq)val}"
    if [[ -n "$val" ]] {
      [[ "${cand1/\\\$/}" != "$cand1" || "${cand1/\\\!/}" != "$cand1" ]] && output+=( "$el$cand2" ) || output+=( "$el$cand1" )
    } elif [[ ${+ice[$el]} = 1 && -n "${nval_ices[(r)$el]}" ]] {
      output+=( "$el" )
    }
  }
  if [[ ${#output} = 0 ]]; then
    builtin print -zr "# No Ice modifiers"
  else
    builtin print -zr "zi ice ${output[*]}; zi "
  fi
  +zi-deploy-message @rst
} || builtin print -r -- "No such plugin or snippet"
