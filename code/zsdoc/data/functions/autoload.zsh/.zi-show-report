setopt localoptions extendedglob warncreateglobal typesetsilent noksharrays
.zi-any-to-user-plugin "$1" "$2"
local user="${reply[-2]}" plugin="${reply[-1]}" uspl2="${reply[-2]}${${reply[-2]:#(%|/)*}:+/}${reply[-1]}"
if [[ "$user/$plugin" != "_dtrace/_dtrace" ]]; then
  .zi-exists-message "$user" "$plugin" || return 1
fi
builtin printf "${ZI[col-title]}Report for${ZI[col-rst]} %s%s plugin\n"\
    "${user:+${ZI[col-uname]}$user${ZI[col-rst]}}${${user:#(%|/)*}:+/}"\
    "${ZI[col-pname]}$plugin${ZI[col-rst]}"
local msg="Report for $user${${user:#(%|/)*}:+/}$plugin plugin"
builtin print -- "${ZI[col-bar]}${(r:${#msg}::-:)tmp__}${ZI[col-rst]}"
local -A map
map=(
  Error:  "${ZI[col-error]}"
  Warning:  "${ZI[col-error]}"
  Note:  "${ZI[col-note]}"
)
() {
  setopt localoptions extendedglob
  builtin print -rl -- "${(@)${(f@)ZI_REPORTS[$uspl2]}/(#b)(#s)([^[:space:]]##)([[:space:]]##)/${map[${match[1]}]:-${ZI[col-keyword]}}${match[1]}${ZI[col-rst]}${match[2]}}"
}
REPLY=""
.zi-diff-functions-compute "$uspl2"
.zi-format-functions "$uspl2"
[[ -n "$REPLY" ]] && builtin print "${ZI[col-p]}Functions created:${ZI[col-rst]}"$'\n'"$REPLY"
REPLY=""
.zi-diff-options-compute "$uspl2"
.zi-format-options "$uspl2"
[[ -n "$REPLY" ]] && builtin print "${ZI[col-p]}Options changed:${ZI[col-rst]}"$'\n'"$REPLY"
REPLY=""
.zi-diff-env-compute "$uspl2"
.zi-format-env "$uspl2" "1"
[[ -n "$REPLY" ]] && builtin print "${ZI[col-p]}PATH elements added:${ZI[col-rst]}"$'\n'"$REPLY"
REPLY=""
.zi-format-env "$uspl2" "2"
[[ -n "$REPLY" ]] && builtin print "${ZI[col-p]}FPATH elements added:${ZI[col-rst]}"$'\n'"$REPLY"
.zi-diff-parameter-compute "$uspl2"
.zi-format-parameter "$uspl2"
[[ -n "$REPLY" ]] && builtin print "${ZI[col-p]}Variables added or redefined:${ZI[col-rst]}"$'\n'"$REPLY"
.zi-find-completions-of-plugin "$user" "$plugin"
typeset -a completions
completions=( "${reply[@]}" )
if [[ "${#completions[@]}" -ge "1" ]]; then
  builtin print "${ZI[col-p]}Completions:${ZI[col-rst]}"
  .zi-check-which-completions-are-installed "${completions[@]}"
  typeset -a installed
  installed=( "${reply[@]}" )
  .zi-check-which-completions-are-enabled "${completions[@]}"
  typeset -a enabled
  enabled=( "${reply[@]}" )
  integer count="${#completions[@]}" idx
  for (( idx=1; idx <= count; idx ++ )); do
    builtin print -n "${completions[idx]:t}"
    if [[ "${installed[idx]}" != "1" ]]; then
      builtin print -n " ${ZI[col-uninst]}[not installed]${ZI[col-rst]}"
    else
      if [[ "${enabled[idx]}" = "1" ]]; then
        builtin print -n " ${ZI[col-info]}[enabled]${ZI[col-rst]}"
      else
        builtin print -n " ${ZI[col-error]}[disabled]${ZI[col-rst]}"
      fi
    fi
    builtin print
  done
  builtin print
fi
