emulate -LR zsh
setopt extendedglob typesetsilent warncreateglobal noshortloops

local url=$1 url1 url2 local_dirA dirnameA svn_dirA local_dirB dirnameB
local -a fileB_there
url="${${url#"${url%%[! $'\t']*}"}%/}"
url1=$url url2=$url

.zi-get-object-path snippet "$url1"
local_dirA=$reply[-3] dirnameA=$reply[-2]
[[ -d "$local_dirA/$dirnameA/.svn" ]] && {
  svn_dirA=".svn"
  if { .zi-first % "$local_dirA/$dirnameA"; } {
    fileB_there=( ${reply[-1]} )
  }
}

.zi-get-object-path snippet "$url2"
local_dirB=$reply[-3] dirnameB=$reply[-2]
[[ -z $svn_dirA ]] && \
  fileB_there=( "$local_dirB/$dirnameB"/*~*.(zwc|md|js|html)(.-DOnN[1]) )
reply=( "$local_dirA/$dirnameA" "$svn_dirA" "$local_dirB/$dirnameB" "${fileB_there[1]##$local_dirB/$dirnameB/#}" )
